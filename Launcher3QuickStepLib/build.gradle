apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        debug {
            keyAlias 'platform'
            keyPassword '123456'
            storeFile file('../keystore/platform.keystore')
            storePassword '12345'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java',
                            'src/main/quickstep_recents_ui_overrides_src',
                            'src/main/quickstep_src',
                            'src/main/src_flags',
                            'src/main/src_shortcuts_overrides']
            res.srcDirs = ['src/main/quickstep_recents_ui_overrides_res', 'src/main/quickstep_res']
            resources.srcDirs = ['src/main/quickstep_recents_ui_overrides_res', 'src/main/quickstep_res']
            manifest.srcFile 'src/main/AndroidManifest-quickstep.xml'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

preBuild {
    doLast {
        def imlFile = file(project.name + ".iml")
        println 'Change ' + project.name + '.iml order'
        try {
            def parsedXml = (new XmlParser()).parse(imlFile)
            def jdkNode = parsedXml.component[1].orderEntry.find { it.'@type' == 'jdk' }
            parsedXml.component[1].remove(jdkNode)
            def sdkString = "Android API " + android.compileSdkVersion.substring("android-".length()) + " Platform"
            new Node(parsedXml.component[1], 'orderEntry', ['type': 'jdk', 'jdkName': sdkString, 'jdkType': 'Android SDK'])
            groovy.xml.XmlUtil.serialize(parsedXml, new FileOutputStream(imlFile))
        } catch (FileNotFoundException e) {
            // nop, iml not found
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly files('../libs/framework.jar')
    implementation project(':SystemUISharedLib')
    implementation project(':launcherprotosnano')
    implementation project(':Launcher3CommonDepsLib')
    implementation project(':SecondaryDisplayLauncherLib')
}
